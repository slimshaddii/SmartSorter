plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

// Stonecutter properties
def mcVersion = stonecutter.current.project
def isNewInputSystem = stonecutter.compare(mcVersion, "1.21.9") >= 0

base {
    archivesName = "smartsorter-${mcVersion}"
}

group = 'net.shaddii.smartsorter'
version = "${project.mod_version}+mc${mcVersion}"

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${mcVersion}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", mcVersion

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": mcVersion
    }
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

tasks.named("jar") {
    archiveBaseName.set("smartsorter")
    archiveVersion.set(project.version.toString())
}

sourceSets {
    main {
        resources {
            srcDir "src/main/resources/generated"
        }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = "smartsorter-${mcVersion}"
            from components.java
        }
    }
}